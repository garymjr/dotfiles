[[ -o interactive ]] || return

# Homebrew environment
if [[ -x /opt/homebrew/bin/brew ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
elif [[ -x /usr/local/bin/brew ]]; then
  eval "$(/usr/local/bin/brew shellenv)"
fi

# Keep PATH tidy and predictable.
typeset -U path PATH
typeset -a _path_prepend _existing_path_prepend
_path_prepend=(
  /usr/local/bin
  "$HOME/go/bin"
  "$HOME/.cargo/bin"
  "$HOME/.bun/bin"
  "$HOME/.local/share/nvim/mason/bin"
  "$HOME/.opencode/bin"
  "$HOME/.local/bin"
  "$HOME/.opencode/bin"
  "/opt/homebrew/opt/libpq/bin"
)
for _p in "${_path_prepend[@]}"; do
  [[ -d "$_p" ]] && _existing_path_prepend+=("$_p")
done
path=("${_existing_path_prepend[@]}" $path)
unset _p _path_prepend _existing_path_prepend

export EDITOR='nvim'
export VISUAL="$EDITOR"
export GIT_EDITOR="$EDITOR"
export KERL_BUILD_DOCS='yes'
export ERL_AFLAGS='-kernel shell_history enabled'
unset MAILCHECK MAIL MAILPATH mailpath
unsetopt MAIL_WARNING 2>/dev/null

[[ -r "$HOME/.zshrc.local" ]] && source "$HOME/.zshrc.local"

HISTFILE="${XDG_STATE_HOME:-$HOME/.local/state}/zsh/history"
mkdir -p "${HISTFILE:h}"
HISTSIZE=100000
SAVEHIST=100000
setopt appendhistory
setopt sharehistory
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt extended_history

setopt autocd
setopt interactive_comments
bindkey -e

autoload -Uz compinit
_zcompdump="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompdump-${ZSH_VERSION}"
mkdir -p "${_zcompdump:h}"
compinit -d "$_zcompdump"
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'

if command -v eza >/dev/null 2>&1; then
  alias ls='eza --group-directories-first --color=always --icons=auto'
  alias ll='eza -lah --group-directories-first --git --color=always --icons=auto'
  alias la='eza -a --group-directories-first --color=always --icons=auto'
  alias l='eza --group-directories-first --color=always --icons=auto'
else
  alias ll='ls -lah'
  alias la='ls -A'
  alias l='ls -CF'
fi
alias cz='chezmoi'
alias lg='lazygit'

ga() {
  local target=""

  if [[ $# -eq 0 ]]; then
    local choice
    choice=$(
      command find .. -maxdepth 1 -type d -name 'agent-*--*' -print 2>/dev/null \
        | sed 's|^\.\./||' \
        | fzf --height=40%
    )
    [[ -z "$choice" ]] && return 1
    target="../$choice"
  else
    local src_pwd src_git origin repo_name name
    src_pwd="$(pwd)"
    src_git="$(command git rev-parse --show-toplevel 2>/dev/null)"
    origin="$(git remote get-url origin)"
    repo_name="$(basename "$origin" .git)"
    name="agent-$repo_name--$1"
    target="$src_pwd/../$name"

    if [[ ! -d "$target" ]]; then
      local ref_repo
      ref_repo="$HOME/git-mirrors/$repo_name.git"

      command mkdir -p "$HOME/git-mirrors"

      if [[ -d "$ref_repo" ]]; then
        git -C "$ref_repo" fetch --all --prune
      else
        git clone --mirror "$origin" "$ref_repo"
      fi

      git clone --reference "$ref_repo" "$origin" "$target" || return 1

      local -a copy_list
      if [[ -n "${GA_COPY:-}" ]]; then
        copy_list=(${=GA_COPY})
      else
        copy_list=(".env")
      fi

      local item
      for item in "${copy_list[@]}"; do
        if [[ -e "$src_pwd/$item" ]]; then
          command rsync -a "$src_pwd/$item" "$target/" \
            || command cp -a "$src_pwd/$item" "$target/" \
            || printf "ga: copy failed: %s\n" "$src_pwd/$item" >&2
        elif [[ -n "$src_git" && -e "$src_git/$item" ]]; then
          command rsync -a "$src_git/$item" "$target/" \
            || command cp -a "$src_git/$item" "$target/" \
            || printf "ga: copy failed: %s\n" "$src_git/$item" >&2
        fi
      done
    fi
  fi

  cd "$target" || return 1
}

gd() {
  local dir base
  dir="$(pwd)"
  base="$(basename "$dir")"

  [[ "$base" == agent-*--* ]] || return 1

  local force=0
  local arg
  for arg in "$@"; do
    [[ "$arg" == "--force" ]] && force=1
  done

  local dirty=0
  if git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
    [[ -n "$(git status --porcelain)" ]] && dirty=1
  fi

  if [[ $dirty -eq 1 && $force -eq 0 ]]; then
    echo "Uncommitted changes. Use gd --force to delete."
    return 1
  fi

  cd .. || return 1
  trash "$base"
}

# mise
if command -v mise >/dev/null 2>&1; then
  eval "$(mise activate zsh)"
fi

# direnv
if command -v direnv >/dev/null 2>&1; then
  eval "$(direnv hook zsh)"
fi

# zoxide
if command -v zoxide >/dev/null 2>&1; then
  eval "$(zoxide init zsh)"
fi

# fzf key bindings and completion
if [[ -t 0 ]] && command -v fzf >/dev/null 2>&1; then
  if fzf --zsh >/dev/null 2>&1; then
    source <(fzf --zsh)
  else
    [[ -r /opt/homebrew/opt/fzf/shell/completion.zsh ]] && source /opt/homebrew/opt/fzf/shell/completion.zsh
    [[ -r /opt/homebrew/opt/fzf/shell/key-bindings.zsh ]] && source /opt/homebrew/opt/fzf/shell/key-bindings.zsh
    [[ -r /usr/local/opt/fzf/shell/completion.zsh ]] && source /usr/local/opt/fzf/shell/completion.zsh
    [[ -r /usr/local/opt/fzf/shell/key-bindings.zsh ]] && source /usr/local/opt/fzf/shell/key-bindings.zsh
    [[ -r "$HOME/.fzf.zsh" ]] && source "$HOME/.fzf.zsh"
  fi
fi

# chezmoi
if command -v chezmoi >/dev/null 2>&1; then
  source <(chezmoi completion zsh 2>/dev/null)
fi

# starship
if command -v starship >/dev/null 2>&1; then
  export STARSHIP_CONFIG="${XDG_CONFIG_HOME:-$HOME/.config}/starship.toml"
  eval "$(starship init zsh)"
fi

# zsh-syntax-highlighting
for _zsh_hl in \
  /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh \
  /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh \
  /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
do
  if [[ -r "$_zsh_hl" ]]; then
    source "$_zsh_hl"
    break
  fi
done
unset _zsh_hl _zcompdump
